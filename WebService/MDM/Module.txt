
Функция GetMasterData(SourceSystem, MasterDataName, SearchParameters)	
	Ответ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("mdm", "GetMasterDataResponse"));
	Ответ.DataResponse = NSI_РаботаСМастерДаннымиПоиск.НайтиДанныеПоУсловиюXDTO(MasterDataName, SearchParameters);
	Если Не ТипЗнч(Ответ.DataResponse.MasterData) = Тип("СписокXDTO") Тогда
		NSI_РаботаСМастерДаннымиРаботаСВнешнимиСистемами.ВернутьОшибкуВебСервиса(Ответ.Errors, "DataNotFound", "По данному условию не найдено мастер-данных.");
	КонецЕсли;
	Возврат Ответ;
КонецФункции

Функция GetMasterDataByKey(SourceSystem, MasterDataName, Keys)
	Ответ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("mdm", "GetMasterDataByKeyResponse"));
	Попытка
		СоответствиеКлюча = Новый Соответствие;
		Для Каждого Ключ Из Keys.Key Цикл
			СоответствиеКлюча.Вставить(Ключ.Key, Ключ.Value);
		КонецЦикла;
		МастерДанные = NSI_РаботаСМастерДаннымиПоиск.НайтиМастерДанныеПоКлючу(СоответствиеКлюча);
		Если МастерДанные = Неопределено Тогда
			NSI_РаботаСМастерДаннымиРаботаСВнешнимиСистемами.ВернутьОшибкуВебСервиса(Ответ.Errors, "DataNotFound", "По данному ключу не найдено мастер-данных.");
			Возврат Ответ;
		КонецЕсли;			
		ОписаниеМастерДанных = NSI_РаботаСМастерДаннымиПоиск.ПолучитьМастерДанныеПоСсылке(МастерДанные);
		Ответ.DataResponse = ОписаниеМастерДанных;
	Исключение
		NSI_РаботаСМастерДаннымиРаботаСВнешнимиСистемами.ВернутьОшибкуВебСервиса(Ответ.Errors, "UnspecifiedError", ОписаниеОшибки());
	КонецПопытки;
	Возврат Ответ;
КонецФункции

Функция GetMasterDataByMethod(SourceSystem, MethodName, MethodParameters)
	Параметры = Новый Структура;
	Для Каждого Параметр Из MethodParameters.Key Цикл
		Параметры.Вставить(Параметр.Key, Параметр.Value);
	КонецЦикла;
	Возврат NSI_РаботаСМастерДаннымиРаботаСВнешнимиСистемами.ВыполнитьВызовМетодаОбработкиДанныхВнешнейСистемы(SourceSystem, MethodName, Параметры);
КонецФункции


Функция GetMasterDataByGUID(SourceSystem, GUID)
	Ответ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("mdm", "GetMasterDataByKeyResponse"));
	МастерДанные = Справочники.NSI_МастерДанные.ПолучитьСсылку(Новый УникальныйИдентификатор(GUID));
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	NSI_ХэшиКлючейМастерДанных.МастерДанные.Ссылка
	                      |ИЗ
	                      |	РегистрСведений.NSI_ХэшиКлючейМастерДанных КАК NSI_ХэшиКлючейМастерДанных
	                      |ГДЕ
	                      |	NSI_ХэшиКлючейМастерДанных.МастерДанные = &МастерДанные
	                      |	И NSI_ХэшиКлючейМастерДанных.МастерДанные.Наименование ЕСТЬ НЕ NULL");
	Запрос.УстановитьПараметр("МастерДанные", МастерДанные);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОписаниеМастерДанных = NSI_РаботаСМастерДаннымиПоиск.ПолучитьМастерДанныеПоСсылке(МастерДанные);
		Ответ.DataResponse = ОписаниеМастерДанных;	
	Иначе
		NSI_РаботаСМастерДаннымиРаботаСВнешнимиСистемами.ВернутьОшибкуВебСервиса(Ответ.Errors, "DataNotFound", "По данному GUID не найдено мастер-данных.");
	КонецЕсли;
	Возврат Ответ;
КонецФункции

