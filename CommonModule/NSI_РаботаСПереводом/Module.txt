Функция ПеревестиСтрокиСервиса(Base, WordList, UseOnlineDictionaries) Экспорт
	ТаблицаПеревода = Новый ТаблицаЗначений;
	ТаблицаПеревода.Колонки.Добавить("LanguageSource", Новый ОписаниеТипов("Строка", ,,,Новый КвалификаторыСтроки(10)));
	ТаблицаПеревода.Колонки.Добавить("LanguageTarget", Новый ОписаниеТипов("Строка", ,,,Новый КвалификаторыСтроки(10)));
	ТаблицаПеревода.Колонки.Добавить("StringSource", Новый ОписаниеТипов("Строка", ,,,Новый КвалификаторыСтроки(500)));
	ТаблицаПеревода.Колонки.Добавить("StringTranslate", Новый ОписаниеТипов("Строка", ,,,Новый КвалификаторыСтроки(500)));
	Для Каждого Строк Из WordList.Strings Цикл
		СтрокаПеревода = ТаблицаПеревода.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПеревода, Строк);
	КонецЦикла;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ИсходнаяТаблицаДляПеревода.LanguageSource,
	                      |	ИсходнаяТаблицаДляПеревода.LanguageTarget,
	                      |	ИсходнаяТаблицаДляПеревода.StringSource
	                      |ПОМЕСТИТЬ ЗапросПеревода
	                      |ИЗ
	                      |	&ТЗ КАК ИсходнаяТаблицаДляПеревода
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ЗапросПеревода.LanguageSource КАК LanguageSource,
	                      |	ЗапросПеревода.LanguageTarget КАК LanguageTarget,
	                      |	ЗапросПеревода.StringSource КАК StringSource,
	                      |	ЕСТЬNULL(lang_Глоссарий.СтрокаНаЯзыкеНазначения, """") КАК StringTranslate,
	                      |	ЛОЖЬ КАК UsedOnlineTranslater
	                      |ИЗ
	                      |	ЗапросПеревода КАК ЗапросПеревода
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.NSI_Глоссарий КАК lang_Глоссарий
	                      |		ПО (lang_Глоссарий.ЯзыкИсходный = ЗапросПеревода.LanguageSource)
	                      |			И (lang_Глоссарий.ЯзыкНазначения = ЗапросПеревода.LanguageTarget)
	                      |			И (lang_Глоссарий.СтрокаНаИсходномЯзыке = ЗапросПеревода.StringSource)");
	Запрос.УстановитьПараметр("ТЗ", ТаблицаПеревода);
	ТЗПолученная = Запрос.Выполнить().Выгрузить();
	ТЗПолученная.Индексы.Добавить("StringTranslate");
	WordList.Strings.Очистить();
	Для Каждого Строк Из ТЗПолученная Цикл
		Если Строк.StringTranslate = "" Тогда
			Продолжить;
		КонецЕсли;
		СтрокаПеревода = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("nsi","TranslateString"));
		ЗаполнитьЗначенияСвойств(СтрокаПеревода, Строк);
		WordList.Strings.Добавить(СтрокаПеревода);
	КонецЦикла;
	Если UseOnlineDictionaries Тогда
		СтрокиДляПоиска = ТЗПолученная.НайтиСтроки(Новый Структура("StringTranslate", ""));
		ssl = Новый ЗащищенноеСоединениеOpenSSL(Новый СертификатКлиентаWindows(), Новый СертификатыУдостоверяющихЦентровWindows());
		Соединение = Новый HTTPСоединение("translate.yandex.net",,,,,60,ssl);
		Для Каждого Строк Из СтрокиДляПоиска Цикл
			Запрос = Новый HTTPЗапрос("/api/v1.5/tr/translate?key=trnsl.1.1.20141014T052152Z.707cf09e19ba7761.8fa2e9e6871ae82a513ed374995ea52a8cd94a1d&text="+КодироватьСтроку(Строк.StringSource, СпособКодированияСтроки.КодировкаURL)+"&lang="+Строк.LanguageSource+"-"+Строк.LanguageTarget); 
			Ответ = Соединение.Получить(Запрос);
			Если Ответ.КодСостояния = 200 Тогда
				СтрокаПеревода = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("nsi","TranslateString"));
				ТелоXML =Ответ.ПолучитьТелоКакСтроку(); 
				Сообщить(ТелоXML);
				Чтение = Новый ЧтениеXML;
				Чтение.УстановитьСтроку(ТелоXML);
				Чтение.ПерейтиКСодержимому();
				ЗаполнитьЗначенияСвойств(СтрокаПеревода, Строк);
				СтрокаПеревода.UsedOnlineTranslater = Истина;
				Если Чтение.Прочитать() И Чтение.Имя = "text" Тогда
					Чтение.Прочитать();
					СтрокаПеревода.StringTranslate = Чтение.Значение;
				Иначе
					СтрокаПеревода.StringTranslate = "";
				КонецЕсли;
				ЗакинутьПереводВГлоссарий(СтрокаПеревода);
				WordList.Strings.Добавить(СтрокаПеревода);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Истина;	
КонецФункции

Функция ЗакинутьПереводВГлоссарий(СтрокаПеревода)
	ЗаписьГлоссария = РегистрыСведений.NSI_Глоссарий.СоздатьМенеджерЗаписи();
	ЗаписьГлоссария.ЯзыкИсходный = СтрокаПеревода.LanguageSource;
	ЗаписьГлоссария.СтрокаНаИсходномЯзыке = СтрокаПеревода.StringSource;
	ЗаписьГлоссария.ЯзыкНазначения = СтрокаПеревода.LanguageTarget;
	ЗаписьГлоссария.СтрокаНаЯзыкеНазначения = СтрокаПеревода.StringTranslate;
	ЗаписьГлоссария.ОнлайнПеревод = Истина;
	ЗаписьГлоссария.Записать();
КонецФункции