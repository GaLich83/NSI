//{Функция ПолучитьОписаниеПолей(ВидМастерДанных)
// Параметры:
// 	ВидМастерДанных - объект, типа СправочникСсылка.NSI_ВидыМД, описание полей и индексов которого необходимо получить.
//	ЭтоГруппа - выводит только реквизиты, используемые для группы, когда Истина
// 		только реквизиты, используемые для элемента, если Ложь,
// 		все реквизиты, если Неопределено
// Функция возвращает таблицу значений с описанием полей из справочника NSI_ОписаниеПолейМД для данного вида мастер-данных
//}
Функция ПолучитьОписаниеПолей(ВидМастерДанных, ЭтоГруппа=Неопределено) Экспорт
	Если ТипЗнч(ВидМастерДанных)=Тип("Строка") Тогда
		ВидМД = Справочники.NSI_ВидыМД.НайтиПоНаименованию(ВидМастерДанных, Истина);
	Иначе
		ВидМД = ВидМастерДанных;
	КонецЕсли;
	НаборИспользованийПолей = Новый Массив;
	НаборИспользованийПолей.Добавить(Перечисления.NSI_ИспользованиеПоляМД.ДляГруппыИЭлемента);
	Если ЭтоГруппа=Неопределено Тогда
		НаборИспользованийПолей.Добавить(Перечисления.NSI_ИспользованиеПоляМД.ДляГруппы);
		НаборИспользованийПолей.Добавить(Перечисления.NSI_ИспользованиеПоляМД.ДляЭлемента);
	ИначеЕсли ЭтоГруппа = Истина Тогда
		НаборИспользованийПолей.Добавить(Перечисления.NSI_ИспользованиеПоляМД.ДляГруппы);
	Иначе
		НаборИспользованийПолей.Добавить(Перечисления.NSI_ИспользованиеПоляМД.ДляЭлемента);
	КонецЕсли;
	ЗапросОписанияПолей = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                                   |	NSI_ВидыМДОписаниеПолей.ИмяПоля,
	                                   |	NSI_ВидыМДОписаниеПолей.ТипПоля,
	                                   |	NSI_ВидыМДОписаниеПолей.ОграниченияПоля,
	                                   |	NSI_ВидыМДОписаниеПолей.ИспользованиеПоля,
	                                   |	NSI_ВидыМДОписаниеПолей.Ключевое,
	                                   |	NSI_ВидыМДОписаниеПолей.Периодическое,
	                                   |	NSI_ВидыМДОписаниеПолей.СинонимПоля,
	                                   |	NSI_ВидыМДОписаниеПолей.НастройкиПоля
	                                   |ИЗ
	                                   |	Справочник.NSI_ВидыМД.ОписаниеПолей КАК NSI_ВидыМДОписаниеПолей
	                                   |ГДЕ
	                                   |	NSI_ВидыМДОписаниеПолей.ИспользованиеПоля В(&НаборИспользованийПолей)
	                                   |	И NSI_ВидыМДОписаниеПолей.Ссылка = &ВидМД");
	ЗапросОписанияПолей.УстановитьПараметр("ВидМД", ВидМД);	
	ЗапросОписанияПолей.УстановитьПараметр("НаборИспользованийПолей", НаборИспользованийПолей);	
	Возврат ЗапросОписанияПолей.Выполнить().Выгрузить();
КонецФункции

Функция ПолучитьМакетМастерДанных(ВидМастерДанных, ЭтоГруппа=Ложь) Экспорт
	ИмяРеквизитаМакета = ?(ЭтоГруппа, "МакетФормыМетаданныхГруппы", "МакетФормыМетаданныхЭлемента");
	СтрокаМакета = ВидМастерДанных[ИмяРеквизитаМакета];
	Если ЗначениеЗаполнено(СтрокаМакета) Тогда
		Возврат NSI_РаботаСМастерДаннымиСервер.ПрочитатьОбъектИзXML(СтрокаМакета);
	Иначе
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;	
КонецФункции

