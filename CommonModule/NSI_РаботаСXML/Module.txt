Функция РазобратьXML(Ч) Экспорт
	Перем Р, ДЗ;
	
	Если ТипЗнч(Ч) = Тип("Строка") Тогда
		ЧтениеОбъекта = Новый ЧтениеXML;
		ЧтениеОбъекта.УстановитьСтроку(Ч);
	Иначе
		ЧтениеОбъекта = Ч;
	КонецЕсли;
	
	Р = Новый Структура();
	Р.Вставить("Кодировка", ЧтениеОбъекта.КодировкаИсточника);
	
	Дерево = Новый ДеревоЗначений();
	Дерево.Колонки.Добавить("Тег");
	Дерево.Колонки.Добавить("Тело");
	Дерево.Колонки.Добавить("Атрибуты");
	Дерево.Колонки.Добавить("Тип");
	Р.Вставить("Дерево", Дерево);
	ТекСтроки = Дерево.Строки;
	РазобратьXMLСлуж(ЧтениеОбъекта, ТекСтроки, Неопределено);	
	
	Возврат Р;
КонецФункции

//Служебная функция для разбора XML в дерево
Функция РазобратьXMLСлуж(Ч, ТекСтроки, Знач ТекСтрока)
	Атрибуты = Новый Структура();
	Пока Ч.Прочитать() Цикл
		Если Ч.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ТекСтрока = ТекСтроки.Добавить();
			ТекСтрока.Тег = Ч.Имя;
			ТекСтрока.Атрибуты = Новый Соответствие();
			Если Ч.КоличествоАтрибутов() > 0 Тогда
				Пока Ч.ПрочитатьАтрибут() Цикл
					ТекСтрока.Атрибуты.Вставить(Ч.Имя, Ч.Значение);
				КонецЦикла
			КонецЕсли;
			РазобратьXMLСлуж(Ч, ТекСтрока.Строки, ТекСтрока);
		ИначеЕсли Ч.ТипУзла = ТипУзлаXML.Текст Тогда
			ТекСтрока.Тело = Ч.Значение;
		ИначеЕсли Ч.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Возврат Неопределено;
		ИначеЕсли Ч.ТипУзла = ТипУзлаXML.ОбъявлениеXML Тогда
			ТекСтрока = ТекСтроки.Добавить();
			ТекСтрока.Тег = Ч.Имя;
			ТекСтрока.Тип = ТипУзлаXML.ОбъявлениеXML;
		ИначеЕсли Ч.ТипУзла = ТипУзлаXML.ИнструкцияОбработки Тогда
			ТекСтрока.Тело = Ч.Значение;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;	
КонецФункции